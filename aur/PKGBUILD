# Maintainer: x34-dzt <https://github.com/x34-dzt>
pkgname=xwlm
pkgver=0.1.2
pkgrel=1
pkgdesc="A TUI for managing Wayland monitor configurations (Hyprland, Sway, River)"
arch=('x86_64')
url="https://github.com/x34-dzt/xwlm"
license=('MIT')
depends=('wayland')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd "xwlm-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "xwlm-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
}

package() {
  cd "xwlm-$pkgver"
  install -Dm755 "target/release/xwlm" "$pkgdir/usr/bin/xwlm"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
